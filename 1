{
  description = "An example project using Flutter";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
    flake-utils.url = "github:numtide/flake-utils";
    flake-compat = {
      url = "github:edolstra/flake-compat";
      flake = false;
    };
  };

  outputs = { self, nixpkgs, flake-utils, ... }@inputs:
    flake-utils.lib.eachDefaultSystem (system:
      let
        pkgs = import nixpkgs {
          inherit system;
          config = {
            allowUnfree = true;
            android_sdk.accept_license = true;
          };
        };
      in {
        devShells.default = let
          android = pkgs.callPackage ./nix/android.nix { };
        in pkgs.mkShell {
          buildInputs = with pkgs; [
            flutter
            jdk11
            android.platform-tools
          ];

          ANDROID_HOME = "${android.androidsdk}/libexec/android-sdk";
          JAVA_HOME = pkgs.jdk11;
          ANDROID_AVD_HOME = "${./.}/.android/avd";
        };
      }
    );
}
